plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// ParchmentMC
	maven { url = "https://maven.parchmentmc.org/" }

	// Botania
	maven { url = "https://maven.blamejared.com/" }

	// Mod Menu
	maven { url = "https://maven.terraformersmc.com/releases/" }

	// Better Combat + Ranged Weapon API
	maven { url = "https://api.modrinth.com/maven/" }

	// Fabric Shield Lib
	maven { url = "https://jitpack.io/" }

	//Cloth Config
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
}

loom {
    splitEnvironmentSourceSets()

	mods {
		"botaniacombat" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	accessWidenerPath = file("src/main/resources/botaniacombat.accesswidener")

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "vazkii.botania:Botania-xplat:${project.botania_version}"

	modImplementation "maven.modrinth:better-combat:${project.better_combat}"

	modImplementation "com.github.Chocohead:Fabric-ASM:v${project.fabricasm_version}"

	modImplementation "com.github.CrimsonDawn45:Fabric-Shield-Lib:v${project.fabric_shield_lib_version}"

	modImplementation "maven.modrinth:ranged-weapon-api:${project.ranged_weapon_api_version}"

	modImplementation "maven.modrinth:archers:${project.archers_version}"
	include(implementation("com.github.ZsoltMolnarrr:TinyConfig:${project.tiny_config_version}"))

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modApi "com.terraformersmc:modmenu:${project.mod_menu_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}

	exclude('**/*.bbmodel')
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}